plugins {
	id 'fabric-loom' version '1.1-SNAPSHOT'
	id "com.modrinth.minotaur" version "2.+"
	id "com.matthewprenger.cursegradle" version "1.4.0"
}

repositories {
    maven {
        url = "https://maven.fabricmc.net/"
    }
    maven {
        url = "https://minecraft.guntram.de/maven/"
    }
    maven {
        url = "https://maven.terraformersmc.com/releases/"
    }
    maven {
        url = "https://maven.shedaniel.me/"
    }
}

sourceCompatibility = 17
targetCompatibility = 17

ext.Versions = new Properties()
Versions.load(file("Versionfiles/mcversion-1.19.4.properties").newReader())

archivesBaseName = "advancementinfo"
ext.projectVersion = "1.3.1"

version = "${Versions['minecraft_version']}-fabric${Versions['fabric_versiononly']}-${project.projectVersion}"

loom {
    accessWidenerPath = file("src/main/resources/advancementinfo.accesswidener")
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

dependencies {
    minecraft  "com.mojang:minecraft:${Versions['minecraft_version']}"
    mappings   "net.fabricmc:yarn:${Versions['yarn_mappings']}:v2"
    modImplementation "net.fabricmc:fabric-loader:${Versions['loader_version']}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${Versions['fabric_version']}"
    modApi("me.shedaniel.cloth:cloth-config-fabric:${Versions['cloth_config_version']}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modCompileOnly "com.terraformersmc:modmenu:${Versions['modmenu_version']}"
}

java {
    withSourcesJar()
}

jar {
    from "LICENSE"
}

modrinth {
    projectId = 'advancementinfo'
    versionName = project.archivesBaseName
    uploadFile = remapJar
}

curseforge {
    apiKey = System.getenv("CURSEFORGE_TOKEN") ?: "0"
    project {
        id = '403815'
        releaseType = 'release'
        addGameVersion("${Versions['minecraft_version']}")
        addGameVersion("Java "+targetCompatibility)
        addGameVersion("Fabric")
        mainArtifact(remapJar)
    }
    options {
        forgeGradleIntegration = false
    }
}
