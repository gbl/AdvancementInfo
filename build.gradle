plugins {
	id 'fabric-loom' version '0.10-SNAPSHOT'
	id "com.modrinth.minotaur" version "1.1.0"
	id "com.matthewprenger.cursegradle" version "1.4.0"
}

repositories {
    maven {
        url = "https://maven.fabricmc.net/"
    }
    maven {
        url = "https://minecraft.guntram.de/maven/"
    }
    maven {
        url = "https://maven.terraformersmc.com/releases/"
    }
    maven {
        url = "https://maven.shedaniel.me/"
    }
}

sourceCompatibility = 17
targetCompatibility = 17

ext.Versions = new Properties()
Versions.load(file("Versionfiles/mcversion-1.18.2.properties").newReader())

archivesBaseName = "advancementinfo"
ext.projectVersion = "1.3.1"

version = "${Versions['minecraft_version']}-fabric${Versions['fabric_versiononly']}-${project.projectVersion}"

loom {
    mixin.defaultRefmapName = "advancementinfo-refmap.json";
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

dependencies {
    minecraft  "com.mojang:minecraft:${Versions['minecraft_version']}"
    mappings   "net.fabricmc:yarn:${Versions['yarn_mappings']}:v2"
    modImplementation "net.fabricmc:fabric-loader:${Versions['loader_version']}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${Versions['fabric_version']}"
    modImplementation ("me.shedaniel.cloth:cloth-config-fabric:6.0.42") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    include ("me.shedaniel.cloth:cloth-config-fabric:6.0.42");
    modCompileOnly "com.terraformersmc:modmenu:${Versions['modmenu_version']}"
}
// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

import com.modrinth.minotaur.TaskModrinthUpload

task publishModrinth (type: TaskModrinthUpload){

    token = System.getenv("MODRINTH_TOKEN")
    projectId = 'G1epq3jN'
    versionNumber = project.version
    versionName = project.archivesBaseName
    releaseType = 'release'
    uploadFile = remapJar // This is the java jar task
    addGameVersion("${Versions['minecraft_version']}")
    addLoader('fabric')
}

curseforge {
    apiKey = System.getenv("CURSEFORGE_TOKEN") ?: "0"
    project {
        id = '403815'
        releaseType = 'release'
        addGameVersion("${Versions['minecraft_version']}")
        addGameVersion("Java "+targetCompatibility)
        addGameVersion("Fabric")
        mainArtifact(remapJar)
    }
    options {
        forgeGradleIntegration = false
    }
}
